(* Translation procedure *)
Require Import TypingFlags.Loader.
Set Type In Type.

From Coq Require Import Bool String List BinPos Compare_dec Omega.
From Equations Require Import Equations DepElimDec.
From Template Require Import All.
From Translation
Require Import util Sorts SAst SLiftSubst SCommon ITyping ITypingLemmata
ITypingAdmissible DecideConversion XTyping Quotes Translation FundamentalLemma
FinalTranslation FullQuote XTypingLemmata IChecking
XChecking Equality plugin_util plugin_checkers.
Import MonadNotation.


(* Not Tail-recursive for the tile being *)
(* TODO Use monad_map? *)
Fixpoint map_tsl Σ axoc l {struct l} : TemplateMonad (list term) :=
  match l with
  | t :: l =>
    match tsl_rec (2 ^ 18) Σ [] t axoc with
    | FinalTranslation.Success _ t =>
      l <- map_tsl Σ axoc l ;;
      ret (t :: l)
    | _ => tmFail "Cannot refine obligation into a Coq term"
    end
  | [] => ret []
  end.

(* Ask the user to prove obligations and returns the corresponding association table *)
(* axoc0 could be used as an accumulator but well... *)
Fixpoint map_lemma (axoc0 : assoc term) (name : ident) (l : list term)
  : TemplateMonad (assoc term) :=
  match l with
  | t :: l =>
    ty <- tmUnquoteTyped Type t ;;
    name <- tmFreshName name ;;
    lem <- tmLemma name (ty : Type) ;;
    tlem <- tmQuote lem ;;
    axoc <- map_lemma axoc0 name l ;;
    ret ((name --> tlem) axoc)
  | [] => ret axoc0
  end.

Fact istrans_nil {Σ} :
  ctxtrans Σ nil nil.
Proof.
  split.
  - constructor.
  - constructor.
Defined.

Definition type_translation {Σ} hg {Γ t A} h {Γ'} hΓ :=
  fst (@complete_translation _ Σ hg) Γ t A h Γ' hΓ.

(* Translation context *)
Record tsl_ctx := {
  Σi : sglobal_context ;
  indt : assoc sterm ;
  constt : assoc sterm ;
  cot : assocn sterm ;
  axoc : assoc term
}.

Definition emptyTC := {|
  Σi := [] ;
  indt := [< >] ;
  constt := [< >] ;
  cot := emptyn ;
  axoc := [< >]
|}.

Notation ε := emptyTC.

Fixpoint tc_ctor_ m (Σ : global_context) ind Θ (ctors : list (prod (prod ident term) nat)) : TemplateMonad tsl_ctx :=
  match ctors with
  | t :: l =>
    Θ <- tc_ctor_ (S m) Σ ind Θ l ;;
    let Σi := Σi Θ in
    let indt := indt Θ in
    let constt := constt Θ in
    let cot := cot Θ in
    let axoc := axoc Θ in
    (* let '(id, ty, m) := t in *)
    let '(pair (pair id ty) _) := t in
    ety <- tmEval lazy (fullquote (2 ^ 18) Σ [] (LiftSubst.subst (tInd ind []) 0 ty) indt constt cot) ;;
    match ety with
    | Success ety =>
      ret {|
          Σi := (decl id ety) :: Σi ;
          indt := indt ;
          constt := constt ;
          cot := aconsn (inductive_mind ind) m (sAx id) cot ;
          axoc := (id --> tConstruct ind m []) axoc
        |}
    | Error e => tmPrint e ;; tmFail "Cannot elaborate to ETT term"
    end
  | [] => ret Θ
  end.

Notation tc_ctor := (tc_ctor_ 0).

(* Get term from ident *)
Definition getTm ident : TemplateMonad term :=
  info <- tmAbout ident ;;
  match info with
  | Some (ConstRef kername) => ret (tConst kername [])
  | Some (IndRef ind) => ret (tInd ind [])
  | Some (ConstructRef ind n) => ret (tConstruct ind n [])
  | None => tmFail ("Unknown " @ ident)
  end.

(* Get the global context from an ident *)
Definition getCtx (ident : ident) : TemplateMonad global_context :=
  tm <- getTm ident ;;
  q  <- tmUnquote tm ;;
  prog <- tmQuoteRec (my_projT2 q : my_projT1 q) ;;
  ret (pair (Datatypes.fst prog) init_graph).

(* Note we could optimise by checking the generated context on the go.
   Then we would carry around the proof that it is correct and we would only
   have to check the extension in Translate.
   Definitely TODO
 *)
Definition TranslateConstant Θ ident : TemplateMonad tsl_ctx :=
  Σ <- getCtx ident ;;
  let Σi := Σi Θ in
  let indt := indt Θ in
  let constt := constt Θ in
  let cot := cot Θ in
  let axoc := axoc Θ in
  info <- tmAbout ident ;;
  match info with
  | Some (ConstRef kername) =>
    entry <- tmQuoteConstant ident false ;;
    match entry with
    | DefinitionEntry {| definition_entry_type := ty |} =>
      ety <- tmEval lazy (fullquote (2 ^ 18) Σ [] ty indt constt cot) ;;
      match ety with
      | Success ety =>
        tmEval all {|
            Σi := (decl kername ety) :: Σi ;
            indt := indt ;
            constt := (kername --> sAx kername) constt ;
            cot := cot ;
            axoc := (kername --> tConst kername []) axoc
          |}
      | Error e => tmPrint e ;; tmFail "Cannot elaborate to ETT term"
      end
    | _ => tmFail ("Not a constant " @ ident)
    end
  | Some (IndRef ({| inductive_mind := kername ; inductive_ind := n |} as ind)) =>
    mind <- tmQuoteInductive kername ;;
    match nth_error (ind_bodies mind) n with
    | Some {| ind_type := ty ; ind_ctors := ctors |} =>
      (* TODO Deal with constructors *)
      ety <- tmEval lazy (fullquote (2 ^ 18) Σ [] ty indt constt cot) ;;
      match ety with
      | Success ety =>
        Θ <- tmEval all {|
            Σi := (decl kername ety) :: Σi ;
            indt := (kername --> sAx kername) indt ;
            constt := constt ;
            cot := cot ;
            axoc := (kername --> tInd ind []) axoc
          |} ;;
        Θ <- tc_ctor Σ ind Θ ctors ;;
        tmEval all Θ
      | Error e => tmPrint e ;; tmFail "Cannot elaborate to ETT term"
      end
    | _ => tmFail "Wrong index of inductive"
    end
  | _ => tmFail ("Not a defined constant" @ ident)
  end.

Definition Translate Θ ident : TemplateMonad () :=
  Σ <- getCtx ident ;;
  let Σi := Σi Θ in
  let indt := indt Θ in
  let constt := constt Θ in
  let cot := cot Θ in
  let axoc := axoc Θ in
  (* First we quote the term to its TC representation *)
  (* TODO We should get the TC global context as well! *)
  entry <- tmQuoteConstant ident false ;;
  match entry with
  | DefinitionEntry {| definition_entry_body := tm ; definition_entry_type := ty |} =>
    (* We get its type and body and elaborate them to ETT terms *)
    (* TODO We should get the correspondence between axioms and Coq constants
       somehow.
     *)
    pretm <- tmEval lazy (fullquote (2 ^ 18) Σ [] tm indt constt cot) ;;
    prety <- tmEval lazy (fullquote (2 ^ 18) Σ [] ty indt constt cot) ;;
    match pretm, prety with
    | Success tm, Success ty =>
      (* We pick the name framework of obligations *)
      obname <- tmEval all (ident @ "_obligation_") ;;
      name <- tmEval all (obname @ "0") ;;
      (* We then typecheck the term in ETT *)
      (* TODO We need a sglobal_context *)
      let ch := ettcheck Σi [] tm ty in
      match ch as o
      return (ch = o -> TemplateMonad ())
      with
      | Some obl => fun (eq : ch = Some obl) =>
        (* obl <- tmEval all obl ;; *)
        (* We now have the list of obligations *)
        (* TODO Check the extended global context is well formed (at least in ITT) *)
        (* We push them into TC *)
        tc_obl <- map_tsl Σ axoc obl ;;
        tc_obl <- tmEval lazy tc_obl ;;
        (* tmPrint tc_obl ;; *)
        (* TODO We then turn them into a list of definitions *)
        (* We ask the user to prove the obligations in Coq *)
        axoc <- map_lemma axoc name tc_obl ;;
        (* Once they are proven we can safely apply soundness to get an ETT
           derivation, but first we need to check the whole global context *)
        (* Σ' <- tmEval lazy (extend [] obname obl) ;; *)
        let Σ' := extend Σi obname obl in
        (* First we check freshness of Σ' *)
        match isallfresh Σ' as b
        return (isallfresh Σ' = b -> TemplateMonad ())
        with
        | true => fun eqf =>
          (* Then we check Σ' in ETT *)
          match ettcheck_ctx Σ' as b
          return (ettcheck_ctx Σ' = b -> TemplateMonad ())
          with
          | true => fun eqcx =>
            (* We now have a derivation of our term in ETT *)
            let hf := isallfresh_sound eqf in
            let xhg := ettcheck_ctx_sound eqcx hf in
            let der := ettcheck_nil_sound obname eq xhg in
            (* der' <- tmEval all der ;; *)
            (* tmPrint der' ;; *)
            (* Next we check the global context makes sense in ITT *)
            match ittcheck_ctx (2 ^ 18) Σ' as b
            return (ittcheck_ctx (2 ^ 18) Σ' = b -> TemplateMonad ())
            with
            | true => fun eqc =>
              let hg := ittcheck_ctx_sound eqc hf in
              let '(_ ; itt_tm ; _) := type_translation hg der istrans_nil in
              t <- tmEval lazy (tsl_rec (2 ^ 18) Σ [] itt_tm axoc) ;;
              match t with
              | FinalTranslation.Success _ t =>
                tname <- tmEval all (ident @ "ᵗ") ;;
                tmMkDefinition tname t ;;
                msg <- tmEval all ("Successfully generated " @ tname) ;;
                tmPrint msg
              | FinalTranslation.Error _ e =>
                msg <- tmEval all ("Cannot translate from ITT to TemplateCoq: " @
                  match e with
                  | FinalTranslation.NotEnoughFuel => "Not enough fuel"
                  | FinalTranslation.TranslationNotFound id => "Not found " @ id
                  | FinalTranslation.TranslationNotHandled => "Translation not handled"
                  | FinalTranslation.TypingError msg _ => "Typing error - " @ msg
                  | FinalTranslation.UnexpectedTranslation s _ _ _ => "Unexpected translation " @ s
                  end) ;;
                tmFail msg
              end
              (* tmPrint "ok" *)
            | false => fun _ => tmFail "Generated global context doesn't typecheck in ITT"
            end eq_refl
          | false => fun _ => tmFail "Generated global context doesn't typecheck in ETT"
          end eq_refl
        | false => fun _ => tmFail "Generated global context has naming conflicts"
        end eq_refl
      | None => fun (_ : ch = None) => tmFail "ETT typechecking failed"
      end eq_refl
    | e1,e2 => tmPrint e1 ;; tmPrint e2 ;; tmFail "Cannot elaborate Coq term to an ETT term"
    end
  | _ => tmFail "Expected definition of a Coq constant"
  end.